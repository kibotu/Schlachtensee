ext {
    VERSION_NAME = simpleReleaseVersionName()
    VERSION_CODE = commitCount()
    CANONICAL_VERSION_NAME = canonicalReleaseVersionName()
    BRANCH_NAME = branchName()
    COMMIT_HASH = commitHash()
    isLocal = project.rootProject.file('local.properties').exists()
    isTesting = false
    BUILD_TIME = new Date().getTime()

    hasCredentials = project.rootProject.file('credentials.properties').exists()

    if (hasCredentials) {
        def Properties properties = new Properties()
        properties.load(project.rootProject.file("credentials.properties").newDataInputStream())
        DEBUG_STORE_PASSWORD = properties.getProperty("debug.store.password", "")
        DEBUG_KEY_PASSWORD = properties.getProperty("debug.key.password", "")
        RELEASE_STORE_PASSWORD = properties.getProperty("release.store.password", "")
        RELEASE_KEY_PASSWORD = properties.getProperty("release.key.password", "")
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

androidExtensions {
    experimental = true
}

kapt {
    useBuildCache = true
}

android {

    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId APPLICATION_ID
        multiDexEnabled true

        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "CANONICAL_VERSION_NAME", escape(CANONICAL_VERSION_NAME)
        buildConfigField "String", "COMMIT_HASH", escape(COMMIT_HASH)
        buildConfigField "String", "BRANCH", escape(BRANCH_NAME)
        buildConfigField "String", "BUILD_DATE", escape(BUILD_TIME)
        buildConfigField "String", "VSC", escape(VSC_PATH)
        buildConfigField "boolean", "IS_LOCAL", escape(isLocal)
        buildConfigField "boolean", "IS_CI", escape(isCi)
        buildConfigField "boolean", "TESTING", escape(isTesting)

        resConfigs "de"
        manifestPlaceholders += [
                crashlyticsEnabled: false
        ]
    }



    // region compile options

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // endregion

    // region compile options

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    // endregion

    // region signing configs

    if (hasCredentials) {

        signingConfigs {
            try {
                debug {
                    storeFile file(DEBUG_KEYSYORE_PATH)
                    storePassword DEBUG_STORE_PASSWORD
                    keyAlias DEBUG_KEYSTORE_ALLIAS
                    keyPassword DEBUG_KEY_PASSWORD
                }
            }
            catch (final ignored) {
                throw new java.security.spec.InvalidKeySpecException('You should define DEBUG_KEYSTORE_ALLIAS, DEBUG_STORE_PASSWORD, DEBUG_KEY_PASSWORD in gradle.properties.')
            }
            try {
                release {
                    storeFile file(RELEASE_KEYSYORE_PATH)
                    storePassword RELEASE_STORE_PASSWORD
                    keyAlias RELEASE_KEYSTORE_ALIAS
                    keyPassword RELEASE_KEY_PASSWORD
                }
            } catch (final ignored) {
                throw new java.security.spec.InvalidKeySpecException('You should define RELEASE_KEYSTORE_ALIAS, RELEASE_STORE_PASSWORD, RELEASE_KEY_PASSWORD in gradle.properties.')
            }
        }
    }

    // endregion

    // region build types

    buildTypes {

        debug {
            if (hasCredentials) {
                signingConfig signingConfigs.debug
            }
            applicationIdSuffix ""
            pseudoLocalesEnabled true
            debuggable true
            minifyEnabled false
            crunchPngs false
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.alwaysUpdateBuildId = false

            aaptOptions {
                ignoreAssetsPattern "!.DS_Store"
            }
        }

        release {
            if (hasCredentials) {
                signingConfig signingConfigs.release
            }
            pseudoLocalesEnabled false
            debuggable false
            minifyEnabled true
            crunchPngs true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders += [crashlyticsEnabled: true]

            aaptOptions {
                ignoreAssetsPattern "!StreamingLogger:!.DS_Store:!README.md:CHANGELOG.md:!Demo:!html"
            }
        }
    }

    // endregion


    // region bundle config

    bundle {
        density {
            enableSplit true
        }
        abi {
            enableSplit true
        }
        language {
            enableSplit false
        }
    }

    // endregion

    // region lint options

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
    }

    // endregion

    // region dex options

    dexOptions {
        javaMaxHeapSize "4g"
        maxProcessCount 8
    }

    // endregion

    // region adb options

    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
        exclude 'META-INF/lib_release.kotlin_module'
    }

    // endregion
}

dependencies {

//    debugImplementation libs.leakcanary2

    implementation libs.supportMultidex

    implementation libs.kotlinJdk8
    implementation libs.kotlinxCoroutinesCore
    implementation libs.kotlinxCoroutinesAndroid

    implementation libs.supportAppCompat
    implementation libs.appCompatResources
    implementation libs.androidKtxCore
    implementation libs.activityKtx
    implementation libs.fragmentKtx
    implementation libs.material
    implementation libs.constraintLayout2
    implementation libs.supportRecyclerview
    implementation libs.supportAnnotations
    implementation libs.supportVectorDrawable
    implementation libs.androidKtxPalette

    // viewmodel and lifecycle
    implementation libs.lifecycleExtensions
    implementation libs.lifecycleViewmodel
    implementation libs.lifecycleLivedata
    kapt libs.lifecycleCompiler
    implementation libs.lifecycleRuntime
    implementation libs.lifecycleRuntimeKtx
    implementation libs.lifecycleLivedataKtx
    implementation libs.androidKtxLifecycleViewmodel

    implementation libs.koinAndroidxScope
    implementation libs.koinAndroidxViewmodel

    implementation libs.androidKtxCollection

    implementation libs.androidKtxNavigationCommon
    implementation libs.androidKtxNavigationFragment
    implementation libs.androidKtxNavigationUi
    implementation libs.androidKtxNavigationRuntime

    implementation libs.playServicesAuth
    implementation libs.firebaseCore
    implementation libs.firebaseCrash
    implementation(libs.crashlytics) { transitive = true }
    implementation libs.firebaseMessaging
    implementation libs.playServicesLocation
    implementation libs.playServicesMaps
    implementation libs.playServicesMapsUtils

    // pagination
    implementation libs.pagingRuntimeKtx

    implementation libs.gson

    implementation libs.rx2java
    implementation libs.rx2Android
    implementation libs.rx2Kotlin
    implementation libs.retrofit
    implementation libs.retrofitConverterGson
    implementation libs.converterSimpleXml // ) { exclude module: "simple-xml" }
    implementation libs.okhttp4
    implementation libs.okhttp3LoggingInterceptor
    implementation libs.okhttp3ResponseEcho

    implementation libs.secureStorage
    implementation libs.rxpermissions

    implementation (libs.exozetCore) {
        exclude group: 'io.realm'
    }

    implementation libs.logger
    implementation libs.deviceInfo
    implementation libs.recyclerViewPresenter4
    implementation libs.recyclerviewAnimators
    implementation libs.processPhoenix

    implementation libs.catLoading

    implementation libs.lottie

    implementation libs.glide
    kapt libs.glideAnnotationsProcessor
}

// region override support library version

configurations.all {

    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'

    // required for gradle uninstallAll
    resolutionStrategy.force androidTestLibs.findBugs
    resolutionStrategy.force androidTestLibs.checkerCompatQual
    resolutionStrategy.force androidTestLibs.checkerQual
    resolutionStrategy.force androidTestLibs.errorProneAnnotations
    resolutionStrategy.force androidTestLibs.guavaListenablefuture

    resolutionStrategy.force libs.material

    resolutionStrategy.force libs.kotlinJdk8
}

// endregion

static def escape(Object value) {
    return "\"$value\""
}

static def escape(boolean value) {
    return "$value"
}

task copyReadme(type: Copy) {
    from "${project.rootDir}/README.md"
    into "${project.rootDir}/app/src/main/assets"
}

task('generateChangelogTask') {
    doLast {
        generateChangelog()
    }
}

preBuild.dependsOn generateChangelogTask
preBuild.dependsOn copyReadme

//apply plugin: 'com.google.gms.google-services'